#+ endpoint: https://graphdb.dumontierlab.com/repositories/shapes-registry
#+ endpoint_in_url: False
#+ summary:  List all active SPARQL endpoints
#+ description: List all active SPARQL endpoints retrieved from SPARQL query file parsing and the number of associated SPARQL queries
#+ tags:
#+   - Get resources
#+ method: GET
# pagination: 100

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <https://schema.org/>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX void-ext: <http://ldf.fi/void-ext#>
SELECT DISTINCT ?sparql_endpoint (count(distinct ?datasets_graph) AS ?datasets_graph_count) (count(distinct ?query_file) AS ?queries_count)
WHERE { 
  ?sparql_endpoint a schema:EntryPoint .
  OPTIONAL {
    ?query_file void:sparqlEndpoint ?sparql_endpoint ;
      schema:query ?query
  }
  OPTIONAL {
    GRAPH ?sparql_endpoint {
      ?datasets_graph a void:Dataset .
      FILTER EXISTS { ?datasets_graph void:propertyPartition ?propertyPartition . }
    } 
  }
} GROUP BY ?sparql_endpoint ORDER BY DESC(?datasets_graph_count) DESC(?queries_count)
